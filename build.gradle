plugins {
	id "java"
	id "idea"
	id "org.sonarqube" version "6.0.1.5171"
	id 'io.freefair.aspectj.post-compile-weaving' version '8.12.1'
	id "jacoco"
	id "jacoco-report-aggregation"
	id "com.diffplug.spotless" version "7.0.2"
	id "org.owasp.dependencycheck" version "8.4.0"
}

defaultTasks 'clean', 'spotlessApply', 'build'

repositories {
	maven {
		url 'https://gds.jfrog.io/artifactory/di-allowed-repos'
	}
}

ext {
	aws_powertools_version = "1.18.0"
	dependencyVersions = [

		// ---------------------------------------------------------
		// cri_common_lib dependencies should match the ipv-cri-lib version
		// Workaround until dependency resolution is fixed.
		// ---------------------------------------------------------
		cri_common_lib_version             : "4.0.0",

		// AWS SDK
		aws_sdk_version                    : "2.30.16",

		aws_lambda_events_version          : "3.14.0",

		// Nimbus Oauth
		nimbusds_oauth_version             : "11.19.1",

		// CRI_LIB powertools
		aws_powertools_logging_version     : "${aws_powertools_version}",
		aws_powertools_metrics_version     : "${aws_powertools_version}",
		aws_powertools_parameters_version  : "${aws_powertools_version}",

		// ---------------------------------------------------------
		// Passport CRI Dependencies
		// ---------------------------------------------------------

		// AWS  aws-lambda-java-libs see https://github.com/aws/aws-lambda-java-libs
		aws_lambda_core_version                    : "1.2.3",
		// Jackson Addons/ needs to track the aws sdk version of jackson
		jackson_version                            : "2.18.3",
		// Code weaving (powertools)
		aspectjrt_version                          : "1.9.22.1",

		// Open telemetry
		opentelemetry_bom_alpha_version  : "2.12.0-alpha",

		// CRI Apache HTTP Client see https://hc.apache.org/httpcomponents-client-4.5.x/current/httpclient/dependencies.html
		httpcomponents_core_version        : "4.4.16",
		httpcomponents_client_version      : "4.5.14",

		// Test
		junit_version                      : "5.11.4",
		hamcrest_version                   : "2.2",
		mockito_version                    : "5.15.2",
		wiremock_version                   : "3.0.1",
		webcompere_version                 : "2.1.6",

		// testFixturesImplementation

		// Contract Tests
		pact_provider_version              : "4.6.16",
		slf4j_log4j12_version              : "2.0.13", // For contract test debug
	]

	// Sets the version used on the lambda + lib (ac tests have separate dependencies)
	javaCompatibility = [
		source : JavaVersion.VERSION_17,
		target : JavaVersion.VERSION_17
	]

	// Code Coverage (Lines/Branches) cannot be below this value on a per sub project basis
	minUnitTestLineCoverage = 0.9
	minUnitTestBranchCoverage = 0.9
}

sonar {
	properties {
		property "sonar.projectKey", "ipv-cri-uk-passport-api"
		property "sonar.organization", "govuk-one-login"
		property "sonar.host.url", "https://sonarcloud.io"
		property "sonar.java.coveragePlugin", "jacoco"
		property "sonar.coverage.jacoco.xmlReportPath", layout.buildDirectory.file("reports/jacoco/reports/reports.xml")
		property "sonar.dependencyCheck.htmlReportPath", layout.buildDirectory.file("reports/dependency-check-report.html")
	}
}

// This generates an aggregate test report at "${buildDir}/reports/jacoco/reports/reports.xml"
reporting {
	reports {
		reports(JacocoCoverageReport) {
			testType = TestSuiteType.UNIT_TEST
		}
	}
}

dependencies {
	jacocoAggregation project(':lib'),
			project(':lib-dvad'),
			project("lambdas:checkpassport"),
			project("lambdas:issuecredential"),
			project("lambdas:certexpiryreminder")
}

spotless {
	java {
		target "**/src/**/*.java"
		googleJavaFormat("1.13.0").aosp()
		importOrder "", "javax", "java", "\\#"
		endWithNewline()
		sourceCompatibility = "${javaCompatibility.source}"
		targetCompatibility = "${javaCompatibility.target}"
	}
	groovyGradle {
		target '**/*.gradle'
		greclipse()
		trimTrailingWhitespace()
		endWithNewline()
	}
}

dependencyCheck {
	failBuildOnCVSS=7
	suppressionFiles= List.of("dependencyCheckAnalyzeSuppressionFiles/Netty-CVE-2023-4586.xml")
	autoUpdate=true
}

subprojects {
	apply plugin: 'org.sonarqube'
	apply plugin: 'org.owasp.dependencycheck'
	apply plugin: 'io.freefair.aspectj.post-compile-weaving'

	repositories {
		maven {
			url 'https://gds.jfrog.io/artifactory/di-allowed-repos'
		}
		//flatDir {
		//	dirs '<Location of your projects absolute path>/di-ipv-cri-lib/build/libs'
		//}
	}

	plugins.withId('java') {
		sourceCompatibility = "${javaCompatibility.source}"
		targetCompatibility = "${javaCompatibility.target}"
	}

	plugins.withId('java-library') {
		sourceCompatibility = "${javaCompatibility.source}"
		targetCompatibility = "${javaCompatibility.target}"
	}

	tasks.withType(JavaCompile).configureEach {
		options.compilerArgs << "-Xlint" << "-Xlint:-processing"
	}

	task allDeps(type: DependencyReportTask) {}
}

clean.doFirst {
	delete "${rootDir}/dist/"
	delete "${rootDir}/.aws-sam"
}
